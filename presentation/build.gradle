apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    signingConfigs {
        test {
            keyAlias '123456'
            keyPassword '123456'
            storeFile file("test.jks")
            storePassword '123456'
        }
    }

    def config = rootProject.ext

    compileSdkVersion config.androidCompileSdkVersion
    buildToolsVersion config.androidBuildToolsVersion
    defaultConfig {
        minSdkVersion config.androidMinSdkVersion
        targetSdkVersion config.androidTargetSdkVersion

        applicationId config.androidApplicationId
        versionCode config.androidVersionCode
        versionName config.androidVersionName
        testInstrumentationRunner config.testInstrumentationRunner
        testApplicationId config.testApplicationId
        archivesBaseName = "$applicationId-v$versionCode($versionName)"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        QA {
            initWith release
            signingConfig signingConfigs.test
            applicationIdSuffix ".QA"
        }
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
        }

    }

    buildFeatures {
        viewBinding = true
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }
    composeOptions {
        kotlinCompilerVersion '1.4.10'
        kotlinCompilerExtensionVersion '1.0.0-alpha05'
    }

    packagingOptions {
        exclude "/META-INF/*.kotlin_module"
        exclude "**/kotlin/**"
        exclude "**/*.txt"
        exclude "**/*.xml"
        exclude "**/*.properties"
    }
}


dependencies {
    implementation project(':domain')
    implementation project(':data')

    implementation 'androidx.compose.ui:ui:1.0.0-alpha05'
    // Tooling support (Previews, etc.)
    implementation 'androidx.ui:ui-tooling:1.0.0-alpha05'
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.0.0-alpha05'
    // Material Design
    implementation 'androidx.compose.material:material:1.0.0-alpha05'
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.0.0-alpha05'
    implementation 'androidx.compose.material:material-icons-extended:1.0.0-alpha05'

    def deps = rootProject.ext

    implementation kotlinStdLib

    implementation deps.appCompat
    implementation deps.material
    implementation deps.cardView
    implementation deps.recyclerView
    implementation deps.recyclerViewFastScroll
    implementation deps.constraintLayout
    implementation deps.supportMedia

    implementation deps.rxAndroid
    implementation deps.rxPermissions

    debugImplementation deps.acraReportDialog

    implementation deps.moxy
    implementation deps.moxyAppcompat
    implementation deps.moxyMaterial
    kapt deps.moxyCompiler

    implementation deps.dagger
    kapt deps.daggerCompiler

    implementation deps.room
    kapt deps.roomCompiler

    implementation deps.glide
    annotationProcessor deps.glideCompiler

    implementation deps.slidr

    implementation deps.chipsLayoutManager

    compileOnly deps.javaxAnnotation

    testImplementation deps.junit
    testImplementation deps.mockito

    androidTestImplementation deps.dexmaker
    androidTestImplementation deps.dexmakerMockito
    androidTestImplementation deps.mockito
    androidTestImplementation deps.espresso
}